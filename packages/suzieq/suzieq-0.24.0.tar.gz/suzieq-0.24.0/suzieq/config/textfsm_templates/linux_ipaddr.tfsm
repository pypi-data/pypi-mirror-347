Value ifindex (\d+)
Value ifname ([^:@]+)
Value _flags (\S+)
Value mtu (\d+)
Value master (\S+)
Value state (\S+)
Value macaddr ([0-9a-f:]+)
Value List ipAddressList ([0-9./]+)
Value List ip6AddressList ([0-9a-f:/]+)
Value description (.*)
Value vni (\d+)
Value vlan (\d+)
Value type (loopback|ether|vlan|vxlan|vrf|bond|bridge|bridge_slave|bond_slave|macvlan)

Start
  ^\d+: -> Continue.Record
  ^${ifindex}: ${ifname}.*: <${_flags}> mtu ${mtu}.* master ${master} state ${state}.*$$
  ^${ifindex}: ${ifname}.*: <${_flags}> mtu ${mtu}.* state ${state}.*$$
  ^\s*link/${type}\s+${macaddr} brd .*$$
  ^\s*inet\s+${ipAddressList}.*scope global.*$$
  ^\s*inet6\s+${ip6AddressList}.*scope global.*$$
  ^\s*alias\s*${description}$$
  ^\s*${type}\s* id ${vni}.*$$
  ^\s*${type}\s*table.*$$
  ^\s*${type}\s*mode.*$$
  ^\s*${type}\s*protocol.*id ${vlan}.*$$
  ^\s*bridge_slave state.*$$
  ^\s*${type} state.* mii_status.*$$
  ^\s*${type} forward_delay.*$$
  ^.*$$ -> Continue
