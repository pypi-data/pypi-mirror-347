Value Required ifname ([^:@ ]+)
Value adminState (up|down)
Value state (up|down)
Value linkUpCnt (\d+)
Value linkUpTimestamp (.*)
Value linkDownCnt (\d+)
Value linkDownTimestamp (.*)
Value vrf (\S+)
Value ifindex (\d+)
Value mtu (\d+)
Value speed (\d+)
Value macaddr ([0-9a-f:]+)
Value List ipAddressList ([0-9./]+)
Value List ip6AddressList ([0-9a-z:/]+)
Value vni (\d+)
Value vlan (\d+)
Value srcVtepIp ([0-9./]*)
Value master (\S+)
Value description (.*)
Value type (loopback|ether|vlan|vxlan|vrf|bond|bridge|bridge_slave|bond_slave|macvlan)

Start
  ^Interface.* is .*$$ -> Continue.Record
  ^Interface\s+${ifname}\s+is ${adminState}.*line protocol is\s+${state}.*$$
  ^Interface\s+${ifname}\s+is ${adminState}.*$$
  ^\s+ Link ups:\s+${linkUpCnt}\s+last:${linkUpTimestamp}$$
  ^\s+ Link downs:\s+${linkDownCnt}\s+last:${linkDownTimestamp}$$
  ^\s+vrf:\s+${vrf}.*$$
  ^\s+Description:\s+${description}.*$$
  ^\s+index\s+${ifindex}.*mtu\s+${mtu}\s+speed\s+${speed}.*$$
  ^\s+HWaddr:\s+${macaddr}.*$$
  ^\s+Interface Type\s+${type}.*$$
  ^\s+inet\s+${ipAddressList}.*$$
  ^\s+inet6\s+${ip6AddressList}.*$$
  ^\s+VLAN\s+Id\s+${vlan}.*$$
  ^\s+VxLAN Id\s+${vni}\s+VTEP IP:\s+${srcVtepIp}\s+Access VLAN Id\s+${vlan}.*$$
  ^\s+Master\s+(${master})\s+ifindex.*$$
  ^\d+:.*$$ -> Continue.Record
  ^${ifindex}:\s+${ifname}.*: <.*> mtu\s+${mtu}.* master ${master} state.*$$
  ^${ifindex}:\s+${ifname}.*: <.*> mtu\s+${mtu}.* state.*$$
  ^\s*link/${type}\s+${macaddr} brd .*$$
  ^\s*inet\s+${ipAddressList}.*scope global.*$$
  ^\s*inet6\s+${ip6AddressList}.*scope global.*$$
  ^\s*alias\s*${description}$$
  ^\s*${type}\s* id ${vni}.*$$
  ^\s*${type}\s*table.*$$
  ^\s*${type}\s*mode.*$$
  ^\s*${type}\s*protocol.*id\s+${vlan}.*$$
  ^\s*${type} state.* mii_status.*$$
  ^\s*${type} forward_delay.*$$
